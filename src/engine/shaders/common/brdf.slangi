#ifndef BRDF_GLSL
#define BRDF_GLSL

#ifndef PI
#define PI 3.1415927h
#endif

struct SurfaceInfo {
    float3 location;

    float3 base_color;

    float3 normal;

    float metalness;

    float roughness;

    float3 emission;
};

float D_GGX(float NoH, float roughness) {
    float k = roughness / (1.h - NoH * NoH + roughness * roughness);
    return k * k * (1.h / PI);
}

float3 F_Schlick(float u, float3 f0, float f90) { return f0 + (f90 - f0) * pow(clamp(1.h - u, 0.h, 1.h), 5.h); }

float V_SmithGGXCorrelated(float NoV, float NoL, float a) {
    float a2 = a * a;
    float GGXL = NoV * sqrt((-NoL * a2 + NoL) * NoL + a2);
    float GGXV = NoL * sqrt((-NoV * a2 + NoV) * NoV + a2);

    return 0.5h / (GGXV + GGXL);
}

float Fd_Lambert() { return 1.h / PI; }

float3 Fd_Burley(float NoV, float NoL, float LoH, float roughness) {
    float f90 = 0.5h + 2.h * roughness * LoH * LoH;
    float3 lightScatter = F_Schlick(NoL, float3(1), f90);
    float3 viewScatter = F_Schlick(NoV, float3(1), f90);

    return lightScatter * viewScatter * (1.h / PI);
}

float PDF_GGX(const in float roughness, const in float3 n, const in float3 l, const in float3 v) {
    float3 h = normalize(v + l);

    const float VoH = clamp(dot(v, h), 0, 1);
    const float NoH = clamp(dot(n, h), 0, 1);

    // D_GGX(NoH, roughness) * NoH / (4.0 + VoH);

    return 1 / (4 * VoH);
}

float3 Fd(in SurfaceInfo surface, float3 l, const float3 v) {
    // Remapping from https://google.github.io/filament/Filament.html#materialsystem/parameterization/remapping
    const float dielectric_f0 = 0.04h; // TODO: Get this from a texture
    const float3 f0 = lerp(dielectric_f0.xxx, surface.base_color.rgb, surface.metalness);

    const float3 diffuse_color = surface.base_color.rgb * (1 - dielectric_f0) * (1 - surface.metalness);

    const float3 h = normalize(v + l);

    float NoV = dot(surface.normal, v) + (float)1e-5;
    float NoL = dot(surface.normal, l);
    const float NoH = clamp(dot(surface.normal, h), 0, 1);
    const float VoH = clamp(dot(v, h), 0, 1);

    if(NoL <= 0) {
        return float3(0);
    }

    NoV = abs(NoV);
    NoL = clamp(NoL, 0, 1);

    // diffuse BRDF
    const float LoH = clamp(dot(l, h), 0, 1);
    return diffuse_color * Fd_Burley(NoV, NoL, LoH, surface.roughness);
}

float3 Fr(in SurfaceInfo surface, float3 l, const float3 v) {
    // Remapping from https://google.github.io/filament/Filament.html#materialsystem/parameterization/remapping
    const float dielectric_f0 = 0.04h; // TODO: Get this from a texture
    const float3 f0 = lerp(dielectric_f0.xxx, surface.base_color.rgb, surface.metalness);

    const float3 h = normalize(v + l);

    float NoV = dot(surface.normal, v) + (float)1e-5;
    float NoL = dot(surface.normal, l);
    const float NoH = clamp(dot(surface.normal, h), 0, 1);
    const float VoH = clamp(dot(v, h), 0, 1);

    if(NoL <= 0) {
        return float3(0);
    }

    NoV = abs(NoV);
    NoL = clamp(NoL, 0, 1);

    const float D = D_GGX(NoH, surface.roughness);
    const float3 F = F_Schlick(VoH, f0, 1.h);
    const float V = V_SmithGGXCorrelated(NoV, NoL, surface.roughness);

    // specular BRDF
    return (D * V) * F;
}

float3 brdf(in SurfaceInfo surface, float3 l, const float3 v) {
    // Rely on the optimizer to remove all the redundant instructions
    return Fd(surface, l, v) + Fr(surface, l, v);
}

#endif
